// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // branch out incremented value in case of inc == 1
    Inc16(in=in2, out=incremented);

    // select between in and incremented (out[t-1])
    Mux16(a=in, b=incremented, sel=inc, out=o1);

    // In case of inc being 1 and load 0, this will output the incremented value
    // else the two inputs will be 'in'
    Mux16(a=o1, b=in, sel=load, out=o2);

    // Reset value if reset == 1, this overrides everything
    Mux16(a=o2, b=false, sel=reset, out=o3);

    // Or 3 selections together because they all can change the value stored in register
    Or(a=inc, b=load, out=sel0);
    Or(a=sel0, b=reset, out=sel1);

    Register(in=o3, load=sel1, out=in2, out=out);

}
